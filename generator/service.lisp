(defpackage #:aws-sdk-cl/generator/service
  (:use #:cl
        #:aws-sdk-cl/generator/operation
        #:aws-sdk-cl/generator/shape
        #:aws-sdk-cl/utils)
  (:import-from #:yason)
  (:export #:generate-all-services
           #:generate-service
           #:load-service
           #:write-service))
(in-package #:aws-sdk-cl/generator/service)

(defpackage #:aws-sdk-cl/generator/service/write)

(defun generate-all-services (&key silent)
  "Generates code for the latest spec file for each service."
  (let ((api-path (asdf:system-relative-pathname :aws-sdk-cl #P"specs/apis/")))
    (dolist (path (uiop:subdirectories api-path))
            (let* ((latest-api-path (car (last (pathname-directory path))))
                   (file (generate-service latest-api-path)))
              (unless silent (format t "~&Generated '~A'~%" file))))))

(defun generate-service (service)
  "Generates the code for the latest spec file for the specified service."
  (let* ((api-dir (asdf:system-relative-pathname :aws-sdk-cl #P"specs/apis/"))
         (out-dir (asdf:system-relative-pathname :aws-sdk-cl #P"services/"))
         (out (make-pathname :defaults out-dir :name service :type "lisp"))
         (service-api-dir (merge-pathnames service api-dir))
         (service-dir (uiop:ensure-directory-pathname service-api-dir))
         (latest-api-path (car (last (uiop:subdirectories service-dir))))
         (api-2.json (merge-pathnames #P"api-2.json" latest-api-path)))
    (ensure-directories-exist out-dir)
    (assert (probe-file api-2.json))
    (to-json-file service api-2.json out)
    out))

(defun load-service (service json)
  "Loads the specified service."
  (uiop:with-temporary-file (:pathname file :direction :output)
                            (to-json-file service json file)
                            (compile-file file)
                            (load file)))

(defun write-service (service json &optional (stream *standard-output*))
  "Writes the code for the specified service using the provided json."
  (let ((*package* (find-package :aws-sdk-cl/generator/service/write))
        (*print-case* :downcase)
        (package-name (make-package-symbol service)))
    (write-package-info stream package-name service)
    (let ((api (yason:parse (uiop:read-file-string json))))
      (loop for name being each hash-key of (gethash "shapes" api)
            using (hash-value options) do
              (format stream "~&~S~%" (compile-shape name options)))

      (loop for action being each hash-key of (gethash "operations" api)
            using (hash-value options)
            for input = (gethash "input" options) do
              (let ((version (get-api-version api))
                    (params (get-params api input)))
                (format stream "~&~S~%" (compile-operation
                                         service
                                         action
                                         version
                                         options
                                         params))))
      (force-output stream))))

(defun get-api-version (api)
  (gethash "apiVersion" (gethash "metadata" api)))

(defun get-params (api input)
  (and input
       (loop for key being each hash-key of
             (let ((shape (gethash "shape" input))
                   (shapes (gethash "shapes" api)))
               (gethash "members" (gethash shape shapes)))
             collect (lispify key))))

(defun make-package-symbol (service)
  (make-symbol (format nil "~:@(~A/~A/~A~)" :aws-sdk-cl :services service)))

(defun to-json-file (service json output)
  (with-open-file (out output :direction :output :if-exists :supersede)
    (write-service service json out)))

(defun write-package-info (stream package-name service)
  (format stream
          "~&;; DO NOT EDIT: File is generated by aws-sdk-cl/GENERATOR.~2%")
  (format stream "~&~S~%"
          `(defpackage ,package-name
             (:nicknames ,(make-symbol (format nil "~:@(~A/~A~)" :aws service)))
             (:import-from #:aws-sdk-cl/generator/shape)
             (:import-from #:aws-sdk-cl/generator/operation)
             (:import-from #:aws-sdk-cl/api)))
  (format stream "~&~S~%" `(in-package ,package-name)))
